#!/usr/bin/perl
#
# gasmailer
#
# Simple script for sending personalised emails
#

use strict;
use warnings;

my $response    = zenity_menu();
do_send($response);

exit;

sub do_send {
    my $type = shift;

    return unless -d $type;

    my $config_ref      = load_yaml("$type/config.yml");
    my $email_address   = zenity_text("Enter email address");

    use File::Slurp;
    my $text            = read_file("$type/email.markdown");

    use Text::Markdown::Hoedown;
    my $html            = markdown($text);

    use Email::Stuffer;
    my $email = Email::Stuffer  ->to($email_address)
                                ->from($config_ref->{from})
                                ->subject($config_ref->{subject})
                                ->html_body($html);

    my @files = glob("$type/attachments/*");
    foreach my $filename (@files) {
        $email->attach_file($filename);
    }

    $email->send;
}

sub load_yaml {
    my $filename = shift;

    use YAML;
    my $config      = read_file($filename);
    my $config_ref  = Load($config);

    return $config_ref;
}

sub zenity_text {
    my $prompt = shift;

    my $response = `zenity --title "GAS Mailer" --entry --text "$prompt"`; 
    chomp($response);

    return $response;
}

sub zenity_menu {

    # fixme: dynamically create options from directories and configs
    my $response = `zenity  --list --title "GAS Mailer" --text "Select an option" --radiolist  --column "Pick" --column "Option" TRUE "Send email to new member"`;

    chomp($response);

    if ($response eq 'Send email to new member') {
        return 'welcome';
    } else {
        print "Unexpect: $response\n";
    }

}
